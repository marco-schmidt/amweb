buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.bmuschko:gradle-tomcat-plugin:2.5'
  }
}

plugins {
  id 'com.github.jk1.dependency-license-report' version '1.11'
  id 'org.owasp.dependencycheck' version '5.2.2'
}

repositories {
  //mavenLocal()
  jcenter()
  mavenCentral()
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse-wtp'
apply plugin: 'com.bmuschko.tomcat'
apply plugin: 'com.github.jk1.dependency-license-report'
apply plugin: 'org.owasp.dependencycheck'

description 'Asset Manager web application'
defaultTasks 'clean', 'war', 'tomcatRun'

dependencies {
  implementation group: 'com.github.marco-schmidt', name: 'am', version: '0.0.1'
  implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.28'
  implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
  testImplementation group: 'junit', name: 'junit', version: '4.12'
  implementation 'com.sun.faces:jsf-api:2.2.20'  
  implementation 'com.sun.faces:jsf-impl:2.2.20'  
  implementation 'javax.servlet:jstl:1.2'  
  implementation 'org.jboss.weld.servlet:weld-servlet-shaded:3.1.2.Final'
  compileOnly 'javax.servlet:servlet-api:2.5'
  def tomcatVersion = '9.0.26'
  tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
           "org.apache.tomcat.embed:tomcat-embed-logging-juli:9.0.0.M6",
           "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"
}

wrapper {
  gradleVersion = '5.6.2'
}

tasks.withType(JavaCompile) {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = sourceCompatibility
  options.debugOptions.debugLevel = 'lines,vars,source'
  options.encoding = java.nio.charset.StandardCharsets.UTF_8
  options.warnings = true
  options.deprecation = true
  options.compilerArgs += ['-Xlint:all', '-Werror']
}

configurations.all {
  resolutionStrategy.eachDependency { DependencyResolveDetails details ->
    if (details.requested.group == 'com.fasterxml.jackson.core' && details.requested.name == 'jackson-databind') {
      details.useVersion '2.9.10'
      details.because 'CVE-2019-16335, CVE-2019-14540'
    }
    if (details.requested.group == 'commons-beanutils' && details.requested.name == 'commons-beanutils') {
      details.useVersion '1.9.4'
      details.because 'CVE-2019-10086'
    }
    if (details.requested.group == 'org.apache.commons' && details.requested.name == 'commons-compress') {
      details.useVersion '1.19'
      details.because 'CVE-2019-12402'
    }
  }
}

dependencyCheck {
  cveValidForHours=24
  failBuildOnCVSS=0
  analyzers {
    assemblyEnabled=false
  }
}

war {
    from('src/main/java') {
        include '*.properties'
        into('WEB-INF/classes')
    }
}
eclipse {
    jdt {
        sourceCompatibility=1.8
        targetCompatibility=1.8
    }

  wtp {
    // https://docs.gradle.org/current/dsl/org.gradle.plugins.ide.eclipse.model.EclipseWtpComponent.html
    component {
      contextPath = 'am'
    }

    // https://docs.gradle.org/current/dsl/org.gradle.plugins.ide.eclipse.model.EclipseWtpFacet.html
    facet {
            facet name: 'jst.java', version: '1.8'
            facet name: 'jst.web', version: '3.0'
    }
  }
}

tomcat {
  httpProtocol = 'org.apache.coyote.http11.Http11Nio2Protocol'
  ajpProtocol  = 'org.apache.coyote.ajp.AjpNio2Protocol'
  httpPort = 8081
}
