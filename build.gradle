buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.bmuschko:gradle-tomcat-plugin:2.7.0'
  }
}

plugins {
  id 'com.diffplug.spotless' version '6.10.0'
  id "com.github.ben-manes.versions" version '0.42.0'
  id 'com.github.jk1.dependency-license-report' version '2.1'
  id 'com.github.spotbugs' version '5.0.10'
  id 'org.owasp.dependencycheck' version '7.1.2'
}

repositories {
  mavenCentral()
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse-wtp'
apply plugin: 'checkstyle'
apply plugin: 'com.bmuschko.tomcat'
apply plugin: 'com.diffplug.spotless'
apply plugin: 'com.github.jk1.dependency-license-report'
apply plugin: 'com.github.spotbugs'
apply plugin: 'org.owasp.dependencycheck'

description 'Asset Manager web application'
defaultTasks 'clean', 'build'

dependencies {
  implementation group: 'com.github.marco-schmidt', name: 'am', version: '0.3.0'
  implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.5'
  implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.11'
  testImplementation group: 'junit', name: 'junit', version: '4.13.2'
  implementation 'org.glassfish:jakarta.faces:4.0.0'
  implementation 'org.jboss.weld.servlet:weld-servlet-core:5.0.1.Final'
  providedCompile 'jakarta.platform:jakarta.jakartaee-api:9.1.0'
  implementation 'javax.websocket:javax.websocket-api:1.1'
  def tomcatVersion = '10.0.23'
  tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
           "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"
}

tasks.withType(JavaCompile) {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = sourceCompatibility
  options.debugOptions.debugLevel = 'lines,vars,source'
  options.encoding = java.nio.charset.StandardCharsets.UTF_8
  options.warnings = true
  options.deprecation = true
  options.compilerArgs += ['-Xlint:all', '-Werror']
}

configurations.all {
  all*.exclude group: 'org.jboss.weld.probe'
  resolutionStrategy.eachDependency { DependencyResolveDetails details ->
    if (details.requested.group == 'com.fasterxml.jackson.core' && details.requested.name == 'jackson-databind') {
      details.useVersion '2.13.2.2'
      details.because 'CVE-2020-36518'
    }
    if (details.requested.group == 'com.google.guava' && details.requested.name == 'guava') {
      details.useVersion '31.1-jre'
      details.because 'CVE-2018-10237'
    }	
    if (details.requested.group == 'org.dom4j' && details.requested.name == 'dom4j') {
      details.useVersion '2.1.3'
      details.because 'CVE-2020-10683'
    }
    if (details.requested.group == 'commons-codec' && details.requested.name == 'commons-codec') {
      details.useVersion '1.13'
      details.because ''
    }
    if (details.requested.group == 'commons-io' && details.requested.name == 'commons-io') {
      details.useVersion '2.8.0'
      details.because 'CVE-2021-29425'
    }
    if (details.requested.group == 'org.apache.httpcomponents' && 
        (details.requested.name == 'fluent-hc' ||
         details.requested.name == 'httpclient' ||
         details.requested.name == 'httpclient-cache' ||
         details.requested.name == 'httpclient-osgi')
       ) {
      details.useVersion '4.5.13'
      details.because 'CVE-2020-13956'
    }
    if (details.requested.group == 'org.apache.commons' && details.requested.name == 'commons-compress') {
      details.useVersion '1.21'
      details.because 'CVE-2021-35515, CVE-2021-35516, CVE-2021-35517, CVE-2021-36090'
    }    
    if (details.requested.group == 'org.xerial' &&
        details.requested.name == 'sqlite-jdbc') {
      details.useVersion '3.39.2.1'
      details.because 'https://github.com/xerial/sqlite-jdbc/issues/562'
    }
    
  }
}

dependencyCheck {
  cveValidForHours=24
  failBuildOnCVSS=10
  suppressionFile=file('config/dependencyCheck/suppressions.xml')
  analyzers {
    assemblyEnabled=false
  }
}
check.dependsOn dependencyCheckAnalyze

// version is stable if it contains only numerical parts or a 'stable keyword'
def isStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase(Locale.ENGLISH).contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return stableKeyword || (version ==~ regex)
}

dependencyUpdates {
  rejectVersionIf {
    !isStable(it.candidate.version)
  }
}

war {
    from('src/main/java') {
        include '*.properties'
        into('WEB-INF/classes')
    }
}

// https://github.com/checkstyle/checkstyle
// http://checkstyle.sourceforge.net
checkstyle {
  toolVersion '9.0'
  ignoreFailures false
  showViolations true
  configDirectory = file("${rootDir}/config/checkstyle")
}

// https://github.com/spotbugs/spotbugs
spotbugs {
  toolVersion = '4.5.2'
  effort = 'max' // min, default, max
  reportLevel = 'low' // low, medium, high  
  ignoreFailures = false
  excludeFilter = file('config/spotbugs/excludeBugsFilter.xml')
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
  reports {
    xml.enabled = false
    html.enabled = true
    html.stylesheet resources.text.fromFile('config/spotbugs/report-stylesheet.xsl')
  }
}

// https://github.com/diffplug/spotless
spotless {
  java {
    licenseHeaderFile 'config/spotless/license-header.txt'
    //removeUnusedImports()
    trimTrailingWhitespace()
    eclipse().configFile 'config/eclipse/formatter.xml'
  }
}

eclipse {
    jdt {
        sourceCompatibility=11
        targetCompatibility=11
    }

  wtp {
    // https://docs.gradle.org/current/dsl/org.gradle.plugins.ide.eclipse.model.EclipseWtpComponent.html
    component {
      contextPath = 'am'
    }

    // https://docs.gradle.org/current/dsl/org.gradle.plugins.ide.eclipse.model.EclipseWtpFacet.html
    facet {
            facet name: 'jst.java', version: '11'
            facet name: 'jst.web', version: '3.0'
    }
  }
}

licenseReport {
  allowedLicensesFile = new File("$projectDir/config/licenseReport/allowed-licenses.json")
}
//check.dependsOn checkLicense

tomcat {
  httpProtocol = 'org.apache.coyote.http11.Http11Nio2Protocol'
  ajpProtocol  = 'org.apache.coyote.ajp.AjpNio2Protocol'
  httpPort = 8081
  contextPath = 'am'
}

task printCache {
  description "print absolute local paths to all dependencies"
  doLast {
    configurations.runtimeClasspath.each { println it }
  }
}
